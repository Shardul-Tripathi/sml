% minimal + latex + comments
metavar termvar, x, y ::=   
  {{ tex \mathit{[[termvar]]} }}

indexvar index, i, j, m ::=

grammar

n :: 'n_' ::= {{ com Const }}

s {{ tex m }} :: 's_' ::= {{ com Secret label }}
  | asecret  :: :: ASecret {{ tex \mathcal{A} }}
  | bsecret  :: :: BSecret {{ tex \mathcal{B} }}

l {{ tex \ell }} :: 'l_' ::= {{ com Label }}
  | public  ::   :: Public  {{ tex \mathcal{P} }}
  | s       ::   :: ASecret
  | lab bt  :: M :: LabelOf {{ tex \mathsf{label}([[bt]]) }}

bt {{ tex \sigma }} :: 'bt_' ::= {{ com Base type }}
  | uint         ::   :: UInt  {{ tex \mathsf{uint} }}
  | bool         ::   :: Bool  {{ tex \mathsf{bool} }}
  | typeof cons  :: M :: Cons  {{ tex \mathsf{typeof}([[cons]]) }}

st {{ tex \psi }} :: 'st_' ::= {{ com Source type }}
  | bt          ::   :: UInt  {{ tex [[bt]] }}
  | array bt n  ::   :: Bool  {{ tex [[bt]][ [[n]] ] }}

t {{ tex \tau }} :: 't_' ::= {{ com Type }}
  | l bt             ::   :: Baset   {{ tex [[bt]]^{[[l]]} }}
  | l bt array n     ::   :: Array   {{ tex [[bt]]^{[[l]]} [ [[n]] ] }}
  | G ( x )          :: M :: EnvLookup
  | l t              :: M :: PublicT {{ tex [ [[t]] ]_{[[l]]} }}

cons {{ tex c }} :: 'cons_' ::= {{ com Constant }}
  | n                             ::   :: Num   {{ tex [[n]] }}
  | true                          ::   :: True  {{ tex \top }}
  | false                         ::   :: False {{ tex \bot }}
  | cons1 + cons2                 :: M :: NAdd
  | cons1 > cons2                 :: M :: NGt
  | ( cons )                      :: M :: Paren
  | get_input i                   :: M :: Inp   {{ tex \mathsf{get\_input}([[i]]) }}

se {{ tex e }} :: 'se_' ::= {{ com Source expression }}
  | cons                          ::  :: Constant
  | x                             ::  :: Var
  | se1 + se2                     ::  :: SAdd   {{ tex [[se1]] + [[se2]] }}
  | cond se se1 se2               ::  :: CondE  {{ tex [[cond]]([[se]], [[se1]], [[se2]]) }}
  | se1 > se2                     ::  :: Relop  {{ tex [[se1]] > [[se2]] }}
  | [ sei ] n                     ::  :: Arr    {{ tex [ [[sei]] ]_{[[n]]} }}
  | x [ se ]                      ::  :: ArrayRead
  | in j                          ::  :: Inp1   {{ tex [[in]]_{[[j]]} }} %\;\;([[i]] \in \{1, 2\}) }}

sc {{ tex s }} :: 'sc_' ::= {{ com Source statement }}
  | st x = se                          ::   :: Decl
  | x := se                            ::   :: Assignment
  | for x in n1 to n2 do sc            ::   :: For  {{ tex [[for]]\;[[x]]\;[[in]]\;[[n1]]\dots[[n2]]\;[[do]]\;[[sc]] }}
  | x [ se1 ] := se2                   ::   :: ArrayWrite
  | if se sc1 sc2                      ::   :: If   {{ tex [[if]]([[se]], [[sc1]], [[sc2]]) }}
  | out se                             ::   :: Output
  | sc1 ; sc2                          ::   :: Sequence
  | loop x until n2 do sc              :: M :: ForI {{ tex [[loop]]\;[[x]]\;[[until]]\;[[n2]]\;[[do]]\; [[sc]] }}

sv {{ tex v }} :: 'sv_' ::= {{ com Source value }}
  | cons                              ::   :: BaseValue
  | [ consi ] n                       ::   :: Array      {{ tex [ [[consi]] ]_{[[n]]} }}
  | srho [ x ]                        :: M :: EnvLookup  {{ tex [[srho]]([[x]]) }}
  | cons n                            :: M :: NthArr     {{ tex [[cons]]_{[[n]]} }}
  | sv [ n mapsto cons ]              :: M :: ArrUpd     {{ tex [[sv]][ [[n]] \mapsto [[cons]] ] }}
  | whateversv                        :: M :: WhatEv     {{ tex \_ }}
  | combine s b1 b2                   :: M :: CombSh     {{ tex \mathcal{D}_{[[s]]}([[b1]], [[b2]]) }}
  | sv + 1                            :: M :: Incr

srho {{ tex \rho }} :: 'srho_' ::= {{ com Source runtime environment }}
  | emp                  ::   :: Empty {{ tex \cdot }}
  | srho , x |-> sv      ::   :: Map   {{ tex [[srho]][ [[x]] \mapsto [[sv]] ] }}
  | srho - x             :: M :: Minus {{ tex [[srho]] \setminus [[x]] }}
  | srho1 restrict srho2 :: M :: Restr {{ tex [ [[srho1]] ]_{\mathsf{dom}([[srho2]])} }}
  | ( srho1 )            :: M :: Paren

sobs {{ tex O }} :: 'sobs_' ::= {{ com Source observation }}
  | emp              ::   :: Emp    {{ tex \cdot }}
  | cons , sobs      ::   :: Value
  | sobs1 , sobs2    :: M :: Seq

defns
JEvalSE :: '' ::=

defn
srho |- se !! sv :: :: seeval :: 'se_' {{ tex [[srho]] \vdash [[se]] \Downarrow [[sv]] }}
by

------------------------- :: const
srho |- cons !! cons

--------------------------- ::var
srho |- x !! srho[x]

i in 1 2 srho |- sei !! ni
----------------------------- :: add
srho |- se1 + se2 !! n1 + n2

srho |- se !! cons
cons = true ==> i = 1
cons = false ==> i = 2
srho |- sei !! sv
------------------------------ :: cond
srho |- cond se se1 se2 !! sv

i in 1 2 srho |- sei !! ni
----------------------------- :: gt
srho |- se1 > se2 !! n1 > n2

i in 0 n srho |- sei !! consi
------------------------------  :: arr
srho |- [sei] n !! [consi] n

srho |- x !! [ consi ] n'
srho |- se !! n
n lt n'
-------------------------------------------- :: aread
srho |- x[se] !! cons n

------------------------ :: inp
srho |- in j !! cons

grammar
formula :: formula_ ::=  
   | judgement             :: :: judgement
   | i in 1 2 formula1     :: :: foralli    {{ tex \forall [[i]] \in \{1, 2\}.\;[[formula1]] }}
   | i in 0 n formula1     :: :: foralln    {{ tex \forall [[i]] \in \{0, [[n]] - 1\}.\;[[formula1]] }}
   | se1 = se2             :: :: seeq
   | sv1 gt sv2            :: :: ngt        {{ tex [[sv1]] > [[sv2]] }}
   | sv1 lt sv2            :: :: nlt        {{ tex [[sv1]] < [[sv2]] }}
   | sv1 geq sv2           :: :: ngeq       {{ tex [[sv1]] \geq [[sv2]] }}
   | sv1 leq sv2           :: :: nleq       {{ tex [[sv1]] \leq [[sv2]] }}
   | v1 gt v2              :: :: ngtv       {{ tex [[v1]] > [[v2]] }}
   | v1 lt v2              :: :: nltv       {{ tex [[v1]] < [[v2]] }}
   | v1 geq v2             :: :: ngeqv      {{ tex [[v1]] \geq [[v2]] }}
   | v1 leq v2             :: :: nleqv      {{ tex [[v1]] \leq [[v2]] }}
   | st1 = st2             :: :: steq
   | i = 1                 :: :: ieqone
   | i = 2                 :: :: ieq2
   | formula1 ==> formula2 :: :: fimp       {{ tex [[formula1]] \Rightarrow [[formula2]] }}
   | cons1 = cons2         :: :: conseq
   | formula1 or formula2  :: :: for        {{ tex [[formula1]] \vee [[formula2]] }}
   | formula1 and formula2 :: :: fand       {{ tex [[formula1]] \wedge [[formula2]] }}
   | ( formula1 )          :: :: paren      {{ tex ( [[formula1]] ) }}
   | default st = sv       :: :: sdefaultt  {{ tex \mathsf{default}([[st]]) = [[sv]] }}
   | v eq sv               :: :: sveqv      {{ tex [[v]] = [[sv]] }}
   | ckt1 = ckt2           :: :: ckteq
   | crho1 = crho2         :: :: crhoeq

defns
JEvalSC :: '' ::=

defn
srho |- sc !! srho' ; sobs :: :: sceval :: 'sc_' {{ tex [[srho]] \vdash [[sc]] \Downarrow [[srho']]; [[sobs]] }}
by

srho |- se !! sv
----------------------------------------- :: decl
srho |- st x = se !! srho, x |-> sv; emp

srho |- se !! sv
--------------------------------------- :: assgn
srho |- x := se !! srho, x |-> sv; emp

srho, x |-> n1 |- loop x until n2 do sc !! srho1; sobs
------------------------------------------------------- :: fort
srho |- for x in n1 to n2 do sc !! srho1 - x ; sobs

srho[x] gt n2
--------------------------------------------- :: loopt
srho |- loop x until n2 do sc !! srho ; sobs

srho[x] leq n2
srho |- sc !! srho1 ; sobs1
(srho1 restrict srho) , x |-> srho1 [x] + 1 |- loop x until n2 do sc !! srho2 ; sobs2
-------------------------------------------------------------------------------------- :: loopi
srho |- loop x until n2 do sc !! srho2 ; sobs1 , sobs2

srho |- x !! [ consi ] n'
srho |- se1 !! n
srho |- se2 !! cons
n lt n'
----------------------------------------------------------------------- :: awrite
srho |- x[se1] := se2 !! srho, x |-> [ consi ] n' [n mapsto cons]; emp

srho |- se !! cons
cons = true ==> i = 1
cons = false ==> i = 2
srho |- sci !! srho'; sobs
-------------------------------------- :: if
srho |- if se sc1 sc2 !! srho'; sobs

srho |- se !! cons
-------------------------------- :: out
srho |- out se !! srho; cons, emp

srho  |- sc1 !! srho1; sobs1
srho1 |- sc2 !! srho2; sobs2
---------------------------------------- :: seq
srho |- sc1; sc2 !! srho2; sobs1, sobs2


defns
JSVTyping :: '' ::=

defn
sv : st :: :: svtyp :: 'v_' {{ tex [[sv]] : [[st]] }}
by

-------------------- :: cons
cons : typeof cons

i in 0 n consi : bt
------------------------- :: arr
[ consi ] n : array bt n

grammar

e {{ tex \widetilde{e} }} :: 'e_' ::=  {{ com Target expression }}
  | cons                          ::   :: Constant
  | x                             ::   :: Var
  | e1 + l e2                     ::   :: SAdd    {{ tex [[e1]] +_{[[l]]} [[e2]] }}
  | cond l e e1 e2                ::   :: CondE   {{ tex [[cond]]_{[[l]]}([[e]], [[e1]], [[e2]]) }}
  | e1 > l e2                     ::   :: Relop   {{ tex [[e1]] >_{[[l]]} [[e2]] }}
  | [ ei ] n                      ::   :: Array   {{ tex [ [[ei]] ]_{[[n]]} }}
  | x [ e ]                       ::   :: ArrayRead
  | in s j                        ::   :: In   {{ tex [[in]]_{[[j]]}^{[[s]]} }} %\;\;([[i]] \in \{1, 2\}) }}
  | e |> s                        ::   :: Coerce  {{ tex [[e]] \rhd [[s]] }}
  | default t                     :: M :: Default {{ tex \mathsf{default}([[t]]) }}

c {{ tex \widetilde{s} }} :: 'c_' ::= {{ com Target statement }}
  | t x = e                            ::   :: Decl
  | x := e                             ::   :: Assignment
  | for x in n1 to n2 do c             ::   :: For  {{ tex [[for]]\;[[x]]\;[[in]]\;[[n1]]\dots[[n2]]\;[[do]]\;[[c]] }}
  | x [ e1 ] := e2                     ::   :: ArrayWrite
  | if e c1 c2                         ::   :: If   {{ tex [[if]]([[e]], [[c1]], [[c2]]) }}
  | out e                              ::   :: Output
  | c1 ; c2                            ::   :: Sequence
  | { c }                              :: M :: Paren
  | loop x until n2 do c               :: M :: ForI {{ tex [[loop]]\;[[x]]\;[[until]]\;[[n2]]\;[[do]]\;[[c]] }}

G {{ tex \Gamma }} :: 'G_' ::= {{ com Type environment }}
  | emp        ::   :: Empty    {{ tex \cdot }}
  | G , x : t  ::   :: Binding
  | whateverG  :: M :: Whatever {{ tex \_ }}
  | G - x      :: M :: MinusX   {{ tex [[G]] \setminus [[x]] }}

formula :: formula_ ::=  
   | t1 = t2               :: :: teq
   | e1 = e2               :: :: eeq
   | c1 = c2               :: :: ceq
   | l1 = l2               :: :: leq
   | v1 = v2               :: :: veq
   | v1 = v2 = v3          :: :: veq3
   | x notin modifies sc   :: :: modifesmem {{ tex [[x]] \not\in \mathsf{modifies}([[sc]]) }}
   | mapsto                :: :: mapsto     {{ tex \mapsto }}
   | l1 ne l2              :: :: labne      {{ tex [[l1]] \neq [[l2]] }}
   | l1 = l2               :: :: labeq
   | base bt1 = base bt2   :: :: baseEq     {{ tex \mathsf{base}([[bt1]]) = \mathsf{base}([[bt2]]) }}
   | cv1 = cv2             :: :: cveq
   | G1 = G2               :: :: gammaeq
   | sv1 = sv2             :: :: sveq
   | b1 b2 = share s cons  :: :: share      {{ tex ([[b1]], [[b2]]) = \mathcal{E}_{[[s]]}([[cons]]) }}
   | r fresh               :: :: rfresh     {{ tex \mathsf{fresh}\;[[r]] }}


defns
JBoundsCheck :: '' ::=

defn
G |= se lt n :: :: bounds :: 'bnd_' {{ tex [[G]] \models [[se]] < [[n]] }}
by


defns
JCompileE :: '' ::=

defn
G |- se : t ~> e :: :: compile :: 's_' {{ tex [[G]] \vdash [[se]] : [[t]] \leadsto [[e]] }}
by

--------------------------------------- :: cons
G |- cons : public typeof cons ~> cons

G(x) = t
---------------- :: var
G |- x : t ~> x

i in 1 2 G |- sei : l uint ~> ei
l = public or l = asecret
------------------------------------------- :: add
G |- se1 + se2 : l uint ~> e1 + l e2

G |- se : l bool ~> e
i in 1 2 G |- sei : l' bt ~> ei
l = public or (l = bsecret and l' = bsecret)
---------------------------------------------------- :: cond
G |- cond se se1 se2 : l' bt ~> cond l e e1 e2

i in 1 2 G |- sei : l uint ~> ei
l = public or l = bsecret
----------------------------------------------- :: gt
G |- se1 > se2 : l bool ~> e1 > l e2

i in 0 n G |- sei : l bt ~> ei
-------------------------------------- :: arr
G |- [sei] n : l bt array n ~> [ei] n

G |- x : l bt array n ~> x
G |- se : public uint ~> e
G |= se lt n
--------------------------- :: aread
G |- x[se] : l bt ~> x[e]

-------------------------------- :: inp
G |- in j : s bt ~> in s j

G |- se : l bt ~> e
-------------------------  :: sub
G |- se : s bt ~> e |> s


defns
JCompileC :: '' ::=

defn
G |- sc ~> c | G' :: :: ccompile :: 'c_' {{ tex [[G]] \vdash [[sc]] \leadsto [[c]] \mid [[G']]}}
by

st = bt ==> t = l bt
st = array bt n ==> t = l bt array n
G |- se : t ~> e
------------------------------------- :: decl
G |- st x = se ~> t x = e | G, x:t

G (x) = l bt
G |- se : l bt ~> e
--------------------------- :: vassgn
G |- x := se ~> x := e | G

G, x:public uint |- loop x until n2 do sc ~> loop x until n2 do c | G, x:public uint
------------------------------------------------------------------------------------- :: for
G |- for x in n1 to n2 do sc ~> for x in n1 to n2 do c | G

G |- x : l bt array n ~> x
G |- se1 : public uint ~> e1
G |- se2 : l bt ~> e2
G |= se1 lt n
--------------------------------------- :: awrite
G |- x [se1] := se2 ~> x[e1] := e2 | G

G |- se : public bool ~> e
i in 1 2 G |- sci ~> ci | whateverG
--------------------------------------- :: if
G |- if se sc1 sc2 ~> if e c1 c2 | G

G |- se : s bt ~> e
-------------------------- :: out
G |- out se ~> out e | G

G |- sc1 ~> c1 | G1
G1 |- sc2 ~> c2 | G2
----------------------------- :: seq
G |- sc1; sc2 ~> c1; c2 | G2

G(x) = public uint
x notin modifies sc
G |- sc ~> c | whateverG
------------------------------------------------------- :: loop
G |- loop x until n2 do sc ~> loop x until n2 do c | G

grammar

r :: 'r_' ::= {{ com Wire id range }}

ckte {{ tex \kappa^{e} }} :: 'ckte_' ::= {{ com Circuit expression }}
  | r                      :: :: Range
  | in s j                 :: :: Inp    {{ tex \mathsf{In}^{[[s]]}_{[[j]]} }}
  | add ckte1 ckte2        :: :: Add    {{ tex \mathsf{Add}([[ckte1]], [[ckte2]]) }}
  | gt ckte1 ckte2         :: :: Gt     {{ tex \mathsf{Gt}([[ckte1]], [[ckte2]]) }}
  | mux ckte ckte1 ckte2   :: :: Mux    {{ tex \mathsf{Mux}([[ckte]], [[ckte1]], [[ckte2]]) }}
  | w |> s                 :: :: Coerce {{ tex [[w]] \rhd [[s]] }}

w {{ tex \widetilde{w} }} :: 'w_' ::= {{ com Compiled base value }}
  | cons                          ::  :: Constant
  | ckte                          ::  :: CircuitE

v {{ tex \widetilde{v} }} :: 'v_' ::= {{ com Compiled value }}
  | w                                 ::   :: BaseValue
  | [ wi ] n                          ::   :: Array      {{ tex [ [[wi]] ]_{[[n]]} }}
  | rho [ x ]                         :: M :: EnvLookup  {{ tex [[rho]]([[x]]) }}
  | w n                               :: M :: NthArr     {{ tex [[w]]_{[[n]]} }}
  | v [ n mapsto w ]                  :: M :: ArrUpd     {{ tex [[v]][ [[n]] \mapsto [[w]] ] }}
  | v + 1                             :: M :: Incr

rho {{ tex \widetilde{\rho} }} :: 'rho_' ::= {{ com Runtime environment }}
  | emp                 ::   :: Empty {{ tex \cdot }}
  | rho , x |-> v       ::   :: Map   {{ tex [[rho]][ [[x]] \mapsto [[v]] ] }}
  | rho - x             :: M :: Minus {{ tex [[rho]] \setminus [[x]] }}
  | rho1 restrict rho2  :: M :: Restr {{ tex [ [[rho1]] ]_{\mathsf{dom}([[rho2]])} }}
  | ( rho1 )            :: M :: Paren

cktc {{ tex \kappa^{s} }} :: 'cktc_' ::= {{ com Circuit command }}
  | emp                    :: :: Emp    {{ tex \cdot }}
  | bind ckte r            :: :: Bind   {{ tex \mathsf{Bind}([[ckte]], [[r]]) }}
  | out ckte               :: :: Out    {{ tex \mathsf{Out}\;[[ckte]] }}
  | cktc1 , cktc2          :: :: Seq

defns
JEvalE :: '' ::=

defn
rho |- e !! v :: :: eeval :: 'ee_' {{ tex [[rho]] \vdash [[e]]\;\tilde{\Downarrow}\;[[v]] }}
by

-------------------- :: const
rho |- cons !! cons

------------------- ::var
rho |- x !! rho[x]

i in 1 2 rho |- ei !! ni
--------------------------------- :: padd
rho |- e1 + public e2 !! n1 + n2

i in 1 2 rho |- ei !! cktei
------------------------------------------ :: sadd
rho |- e1 + asecret e2 !! add ckte1 ckte2

rho |- e !! cons
cons = true ==> i = 1
cons = false ==> i = 2
rho |- ei !! v
-------------------------------- :: pcond
rho |- cond public e e1 e2 !! v

rho |- e !! ckte
i in 1 2 rho |- ei !! cktei
--------------------------------------------------- :: scond
rho |- cond bsecret e e1 e2 !! mux ckte ckte1 ckte2

i in 1 2 rho |- ei !! ni
--------------------------------- :: pgt
rho |- e1 > public e2 !! n1 > n2

i in 1 2 rho |- ei !! cktei
----------------------------------------- :: sgt
rho |- e1 > bsecret e2 !! gt ckte1 ckte2

rho |- ei !! wi
------------------------- :: arr
rho |- [ei] n !! [wi] n

rho |- e !! n
rho |- x !! [ wi ] n'
n lt n'
----------------------- :: aread
rho |- x[e] !! w n

------------------------------- :: inp
rho |- in s j !! in s j

rho |- e !! w
------------------------ :: coerce
rho |- e |> s !! w |> s

defns
JEvalC :: '' ::=

defn
rho |- c !! rho' ; cktc :: :: ceval :: 'ec_' {{ tex [[rho]] \vdash [[c]]\;\tilde{\Downarrow}\;[[rho']]; [[cktc]] }}
by

rho |- e !! v
v = cons or v = [consi] n
------------------------------------ :: decl
rho |- t x = e !! rho, x |-> v; emp

rho |- e !! ckte
r fresh
-------------------------------------------- :: declckt
rho |- t x = e !! rho, x |-> r; bind ckte r

rho |- e !! [cktei] n
i in 0 n ri fresh
--------------------------------------------------- :: declcktarr
rho |- t x = e !! rho, x |-> [ri] n; bind cktei ri

% rho |- e !! v
% v = cons or v = [consi] n
% ------------------------------------ :: assgn
% rho |- x := e !! rho, x |-> v; emp

% rho |- e !! ckte
% r = next_range ()
% -------------------------------------------- :: assgnckt
% rho |- x := e !! rho, x |-> r; bind ckte r

% rho |- e !! [cktei] n
% i in 0 n ri = next_range ()
% --------------------------------------------------- :: assgncktarr
% rho |- x := e !! rho, x |-> [ri] n; bind cktei ri

rho, x |-> n1 |- loop x until n2 do c !! rho1; cktc
---------------------------------------------------- :: fort
rho |- for x in n1 to n2 do c !! rho1 - x ; cktc

rho[x] gt n2
----------------------------------------- :: loopt
rho |- loop x until n2 do c !! rho ; emp

rho[x] leq n2
rho |- c !! rho1 ; cktc1
(rho1 restrict rho) , x |-> rho1 [x] + 1 |- loop x until n2 do c !! rho2 ; cktc2
--------------------------------------------------------------------------------- :: loopi
rho |- loop x until n2 do c !! rho2 ; cktc1, cktc2

rho |- x !! [ wi ] n'
rho |- e1 !! n
rho |- e2 !! w
n lt n'
------------------------------------------------------------- :: awrite
rho |- x[e1] := e2 !! rho, x |-> [ wi ] n' [n mapsto w]; emp

rho |- e !! cons
cons = true ==> i = 1
cons = false ==> i = 2
rho |- ci !! rho'; cktc
-------------------------------- :: if
rho |- if e c1 c2 !! rho'; cktc

rho |- e !! ckte
------------------------------- :: out
rho |- out e !! rho; out ckte

rho  |- c1 !! rho1; cktc1
rho1 |- c2 !! rho2; cktc2
------------------------------------ :: seq
rho |- c1; c2 !! rho2; cktc1, cktc2

grammar

b :: 'cv_' ::= {{ com Share (byte string) }}
  | crho [ r ]       :: M :: Lookup

crho {{ tex \widehat{\rho} }} :: 'crho_' ::= {{ com Circuit environment }}
  | emp                ::   :: Empty {{ tex \cdot }}
  | crho , r |-> b     ::   :: Map   {{ tex [[crho]][ [[r]] \mapsto [[b]] ] }}

defns
JEvalCircE :: '' ::=

defn
crho1 , crho2 |- ckte !! b1 , b2 :: :: circeeval :: 'ckte_' {{ tex [[crho1]], [[crho2]] \vdash [[ckte]] \Downarrow [[b1]], [[b2]] }}
by


---------------------------------------- :: r
crho1, crho2 |- r !! crho1[r], crho2[r]

b1 b2 = share s cons
------------------------------------ :: in
crho1, crho2 |- in s j !! b1, b2

crho1, crho2 |- ckte1 !! b11, b21
crho1, crho2 |- ckte2 !! b12, b22
n1 = combine asecret b11 b21
n2 = combine asecret b12 b22
b1 b2 = share asecret n1 + n2
------------------------------------------ :: add
crho1, crho2 |- add ckte1 ckte2 !! b1, b2

crho1, crho2 |- ckte1 !! b11, b21
crho1, crho2 |- ckte2 !! b12, b22
n1 = combine bsecret b11 b21
n2 = combine bsecret b12 b22
b1 b2 = share bsecret n1 > n2
------------------------------------------ :: gt
crho1, crho2 |- gt ckte1 ckte2 !! b1, b2


crho1, crho2 |- ckte !! b1, b2
crho1, crho2 |- ckte1 !! b11, b21
crho1, crho2 |- ckte2 !! b12, b22
cons = combine bsecret b1 b2
cons1 = combine bsecret b11 b21
cons2 = combine bsecret b12 b22
cons = true ==> b1' b2' = share bsecret cons1
cons = false ==> b1' b2' = share bsecret cons2
------------------------------------------------ :: mux
crho1, crho2 |- mux ckte ckte1 ckte2 !! b1', b2'

b1 b2 = share s cons
------------------------------------ :: coercec
crho1, crho2 |- cons |> s !! b1, b2

crho1, crho2 |- ckte !! b1, b2
cons = combine s1 b1 b2
b1' b2' = share s cons
------------------------------------- :: coercer
crho1, crho2 |- ckte |> s !! b1', b2'

defns
JEvalCircC :: '' ::=

defn
crho1 , crho2 |- cktc !! crho1' , crho2' ; sobs :: :: circceval :: 'cktc_' {{ tex [[crho1]], [[crho2]] \vdash [[cktc]] \Downarrow [[crho1']], [[crho2']]; [[sobs]] }}
by

------------------------------------------  :: emp
crho1, crho2 |- emp !! crho1, crho2; emp

crho1, crho2 |- ckte !! b1, b2
---------------------------------------------------------------------  :: bind
crho1, crho2 |- bind ckte r !! crho1, r |-> b1, crho2, r |-> b1; emp

crho1, crho2 |- ckte !! b1, b2
cons = combine s b1 b2
----------------------------------------------------  :: out
crho1, crho2 |- out ckte !! crho1, crho2; cons, emp

crho1, crho2 |- cktc1 !! crho1', crho2'; sobs1
crho1', crho2' |- cktc2 !! crho1'', crho2''; sobs2
-------------------------------------------------------------- :: seq
crho1, crho2 |- cktc1, cktc2 !! crho1'', crho2''; sobs1, sobs2

defns
JSTypeTypeEquiv :: '' ::=

defn
st eqv t :: :: sttequiv :: 'st_' {{ tex [[st]] \sim [[t]] }}
by

------------ :: bt
bt eqv l bt

---------------------------- :: arr
array bt n eqv l bt array n

% defns
% JSEnvGammaEquiv :: '' ::=

% defn
% G ~ srho :: :: senvgammaequiv :: 'sen_' {{ tex [[G]] \sim [[srho]] }}
% by

% ---------- :: emp
% emp ~ emp

% sv : st
% st eqv t
% G ~ srho
% ----------------------- :: bnd
% G, x:t ~ srho, x |-> sv


defns
JTEnvCompile :: '' ::=

defn
G |- srho ~> rho ; crho1 , crho2 :: :: envcompile :: 'en_' {{ tex [[G]] \vdash [[srho]] \hookrightarrow [[rho]]; [[crho1]], [[crho2]] }}
by

-------------------------------- :: emp
emp |- emp ~> emp; crho1, crho2

G |- srho - x ~> rho; crho1, crho2
srho[x] = cons
cons : bt
---------------------------------------------------------- :: pbt
G , x:public bt |- srho ~> rho, x |-> cons; crho1, crho2

G |- srho - x ~> rho; crho1, crho2
srho[x] = cons
cons : bt
r fresh
b1 b2 = share s cons
--------------------------------------------------------------------- :: sbt
G , x:s bt |- srho ~> rho, x |-> r; crho1, r |-> b1, crho2, r |-> b2

G |- srho - x ~> rho; crho1, crho2
srho[x] = [consi] n
i in 0 n consi : bt
----------------------------------------------------------------------- :: parr
G, x:public bt array n |- srho ~> rho, x |-> [ consi ] n; crho1, crho2

G |- srho - x ~> rho; crho1, crho2
srho[x] = [consi] n
i in 0 n (consi : bt and (ri fresh and b1i b2i = share s consi))
---------------------------------------------------------------------------------------- :: sarr
G , x:s bt array n |- srho ~> rho, x |-> [ ri ] n; crho1, ri |-> b1i, crho2, ri |-> b2i
